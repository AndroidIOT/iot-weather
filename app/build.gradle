apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

class Config {
    Properties properties
}

ext {
    config = new Config()
    config.properties = new Properties()
    def basePath = rootDir.absolutePath + "/config/"
    def propertiesFilename = "common.properties"
    config.properties.load(new FileInputStream(rootProject.file((basePath + propertiesFilename))))
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "moj.iot.weather"
        minSdkVersion 24
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "DARK_SKY_ENDPOINT", "\"${config.properties.dark_sky_endpoint}\""
        buildConfigField "double", "DEFAULT_DEVICE_LATITUDE", "${config.properties.default_device_latitude}"
        buildConfigField "double", "DEFAULT_DEVICE_LONGITUDE", "${config.properties.default_device_longitude}"
        resValue "string", "dark_sky_key", "${config.properties.dark_sky_key}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    jUnitVersion = "4.12"
    androidThingsVersion = "0.1-devpreview"
    androidThingsContribVersion = "0.1"
    timberVersion = "4.4.0"
    daggerVersion = "2.8"
    rxJavaVersion = "2.0.1"
    retrofitVersion = "2.1.0"
    okHttpVersion = "3.5.0"
    jodaTimeVersion = "2.9.7"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile "junit:junit:$jUnitVersion"

    provided "com.google.android.things:androidthings:$androidThingsVersion"
    compile "com.google.android.things.contrib:driver-rainbowhat:$androidThingsContribVersion"

    compile "com.jakewharton.timber:timber:$timberVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    compile "io.reactivex.rxjava2:rxandroid:$rxJavaVersion"

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    compile "joda-time:joda-time:$jodaTimeVersion"
}
